Module:
	concept += Concept
;
Concept:
	Object|View|Page
;

Size:
	'small'|'medium'|'large'
;

Color:
	'red'|'blue'|'green'|'black'|'white'|'yellow'
;
Style:
	('color' '=' Color) |
	('size' '=' Size)
;

View:
	'view' name=ID ':' 
		views *= SubView
	';'
;

SubView:
    RowSeparator | ViewInView
;

ViewOnPage:
	selector=Selector 'position' '=' position=/[^\s]+/ (Style)*
;

ViewInView:
	selector=Selector ('at' position=NUMBER)? ('size' size=NUMBER)?
;

Page:
	'page' name=ID ':' 
		'title' '=' title=STRING
		(layout=Layout | menubar=Menubar | sidebar=Sidebar | footer=Footer)*
		views *= ViewOnPage
	';'
;

Selector:
	  SelectorObject|SelectorView|DataShow|Form
;

SelectorObject:
	 ('show')? object=[Object] '.' property=[Property]
;

Form:
	'form' 'for' obj=[Object] 'action' '=' action=FormAction 
;

FormAction:
	'create'|'update'
;

SelectorView:
	'show' view=[View]
;

DataShow:
	type=[View] 'data_show' '=' '{' data+=SelectorObject[","]  '}'
;

HtmlClass:
	'class' '=' '{' htmlClasses += HtmlClassValue[","]  '}'
;

HtmlClassValue:
	ClassKeyPair|ClassKey
;

ClassKey:
	key=STRING
;

ClassKeyPair:
	key=STRING ':' value=STRING
;

PropertyList:
	'parameters' '=' '{' propertyList += STRING[","] '}'
;

ReferencedParameter:
	'@' page=[Page]
;

Property:
	name=ID type=[View] 'label' '=' label=STRING (params=PropertyList | ref_param=ReferencedParameter )? (classes = HtmlClass)? ('ref' '=' ref=STRING)?
;

Object:
	'object' name=ID ':'
		properties *= Property
	';'
;

Layout:
	'layout' name = ID positions *= STRING
;

RefLink:
    label=STRING ref=ReferencedParameter|( 'ref' '=' aps_ref=STRING)
;

Sidebar:
    'sidebar' ':'
        links*=RefLink
    ';'
;

Footer:
	'footer' paragraph=STRING (side=Side)?
;

Side:
	'left'|'right'
;

Menu:
    ('menu' name=ID (side=Side)? ':'
        items*=RefLink 
    ';')| ref = RefLink (side=Side)?
;

Menubar:
    'menubar' (brandName=STRING)? ':'
        menus*=Menu
    ';'
;

RowSeparator:
    '!row'
;

Row:
    'row' ':'
        selectors*=ViewInView
    ';'
;

// Special rule for comments. Comments start with //
Comment:
  /\/\/.*$/
;