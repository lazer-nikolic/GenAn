Module:
	concept += Concept
;
Concept:
	Object|View|Page|Footer|Menubar|Sidebar
;

Size:
	'small'|'medium'|'large'
;

Color:
	'red'|'blue'|'green'|'black'|'white'|'yellow'|'gray'
;
Style:
	('color' '=' Color) |
	('size' '=' Size)
;

View:
	'view' name=ID ':' 
		views *= SubView
	';'
;

SubView:
    RowSeparator | ViewInView
;

ViewOnPage:
	selector=Selector 'position' '=' position=/[^\s]+/ (Style)*
;


ViewInView:
	selector=Selector ('at' position=NUMBER)? ('size' size=NUMBER)?
;

Page:
	'page' name=ID ':' 
		'title' '=' title=STRING
		(using *= Use)
		(layout=Layout)*
		views *= ViewOnPage
	';'
;

Use:
	('use' 'menubar' m=[Menubar])|('use' 'sidebar' s=[Sidebar])|('use' 'footer' f=[Footer])	
;

Selector:
	  SelectorObject|SelectorView|DataShow|Form|Jumbo|Paragraph|StaticLink
;

StaticLink:
	'link' stLink=RefLink (classes = HtmlClass)?
;

SelectorObject:
	 ('show')? object=[Object] '.' property=[Property]
;

Form:
	'form' 'for' obj=[Object] 'actions' '=' actions+=FormAction
;

FormAction:
	'create'|'update'|'delete'|'view'|'resetInput'
;

SelectorView:
	'show' view=[View]
;

DataShow:
	type=[View] 'data_show' '=' '{' data+=SelectorObject[","]  '}'
;

Paragraph:
	'paragraph' paragraph = STRING 
;

Jumbo:
	'jumbo' ':' (jumbo += JumboParameters)
;

JumboParameters:
	('heading' '=' heading=STRING) | ('text' '=' text=STRING) | ('{'ref = RefLink '}')| ('imgPath' '=' imgPath=STRING)
;

HtmlClass:
	'class' '=' '{' htmlClasses += HtmlClassValue[","]  '}'
;

HtmlClassValue:
	ClassKeyPair|ClassKey
;

ClassKey:
	key=STRING
;

ClassKeyPair:
	key=STRING ':' value=STRING
;

PropertyList:
	'parameters' '=' '{' propertyList += STRING[","] '}'
;

ReferencedParameter:
	'@' page=[Page]
;

Property:
	name=ID type=[View] 'label' '=' label=STRING (additionalParameters*=AdditionParameters)
;

AdditionParameters:
	(params=PropertyList | ref_param=ReferencedParameter)| (classes = HtmlClass)|('ref' '=' ref=STRING)
;

Object:
	'object' name=ID ':'
		properties *= Property
		queries *= Query
	';'
;

Query:
    'query' name=ID ( 'property''='property=[Property] 'condition''='condition=Condition)? ('sortBy''='sortBy=[Property])? ('order''='order=Order)? ('rangeFrom''='rangeFrom=INT)? ('rangeTo''='rangeTo=INT)?
;

Condition:
    StringCondition | IntegerCondition
;

StringCondition:
    conditionName=StringConditionName '$' parameter=STRING
;

IntegerCondition:
    conditionName=IntegerConditionName '$' parameter=INT
;

StringConditionName:
    'startsWith'|'endsWith'|'matches'
;

IntegerConditionName:
    'lt'|'gt'|'le'|'ge'|'e'
;

Order:
    'asc'|'desc'
;

Layout:
	'layout' name = ID positions *= STRING
;

RefLink:
    label=STRING ref=ReferencedParameter|( 'ref' '=' aps_ref=STRING)
;

Sidebar:
    'sidebar' name=ID ':'
    	('color' '=' color=Color)?
        links*=RefLink
    ';'
;

Footer:
	'footer' name=ID paragraph=STRING (side=Side)? ('color' '=' color=Color)? ';'
;

Side:
	'left'|'right'
;

Menu:
    ('menu' name=ID (side=Side)? ':'
        items*=RefLink 
    ';')| ref = RefLink (side=Side)?
;

Menubar:
    'menubar' name=ID  (brandName=STRING)? ':'
    	('color' '=' color=Color)?
        menus*=Menu
    ';'
;

RowSeparator:
    '!row' (number=INT)?
;

Row:
    'row' ':'
        selectors*=ViewInView
    ';'
;

// Special rule for comments. Comments start with //
Comment:
  /\/\/.*$/
;