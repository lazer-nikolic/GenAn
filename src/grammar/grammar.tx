Module:
	concept += Concept
;
Concept:
	Object|View|Page
;

Size:
	'small'|'medium'|'large'
;

Color:
	'red'|'blue'|'green'|'black'|'white'|'yellow'
;
Style:
	('color' '=' Color) |
	('size' '=' Size)
;

View:
	'view' name=ID ':' 
		views *= Selector
	';'
;
ViewOnPage:
	selector=Selector 'position' '=' position=/[^\s]+/ (Style)*
;

Page:
	'page' name=ID ':' 
		'title' '=' title=STRING
		('layout' '=' layout=[Layout])?
		(sidebar=Sidebar)?
		views *= ViewOnPage
	';'
;

Selector:
	  SelectorObject|SelectorView|DataShow
;

SelectorObject:
	 ('show')? object=[Object] '.' property=[Property]
;

SelectorView:
	'show' view=[View]
;

DataShow:
	type=[View] 'data_show' '=' '{' data+=SelectorObject[","]  '}'
;

HtmlClass:
	'class' '=' '{' htmlClasses += HtmlClassValue[","]  '}'
;

HtmlClassValue:
	ClassKeyPair|ClassKey
;

ClassKey:
	key=STRING
;

ClassKeyPair:
	key=STRING ':' value=STRING
;

PropertyList:
	'parameters' '=' '{' propertyList += STRING[","] '}'
;

ReferencedParameter:
	'@' page=[Page]
;

Property:
	name=ID type=[View] 'label' '=' label=STRING (params=PropertyList | ref_param=ReferencedParameter )? (classes = HtmlClass)? ('ref' '=' ref=STRING)?
;

Object:
	'object' name=ID ':'
		properties *= Property
	';'
;

Layout:
	name = ID positions *= STRING
;

RefLink:
    label=STRING ref=ReferencedParameter
;

Sidebar:
    'sidebar' ':'
        links*=RefLink
    ';'
;

// Special rule for comments. Comments start with //
Comment:
  /\/\/.*$/
;